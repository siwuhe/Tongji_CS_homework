; ===============================
; 初始化常量和状态
; ===============================

    SUB  R1, R1, R1
    ADDI R1, 0x40      ; N_FLOORS = 64

    SUB  R2, R2, R2
    ADDI R2, 0x26      ; F_TRUE = 38

    SUB  R3, R3, R3    ; total_up = 0
    SUB  R4, R4, R4    ; total_down = 0
    SUB  R5, R5, R5    ; broken_eggs = 0
    SUB  R6, R6, R6    ; total_throws = 0

    ADD  R7, R1, R0    ; high = 64
    SUB  R8, R8, R8
    ADDI R8, 0x1       ; low = 1

    ADD  R9, R1, R0    ; last_floor = N_FLOORS

    SUB  R12, R12, R12 ; result_F = 0
    SUB  R14, R14, R14 ; cost_scarcity
    SUB  R15, R15, R15 ; cost_labor

; ===============================
; Phase 1: 二分查找 while (high - low >= 2)
; ===============================

PHASE1_CHECK:
    SUB  R11, R7, R8
    SUBI R11, 0x2
    CMP  R13, R11, R0
    BN   PHASE1_END
    ; diffHL >=2 → 进入循环体

PHASE1_LOOP:
    ADDI R6, 0x1       ; total_throws++

    ADD  R10, R7, R8
    SHRI R10, R10, 0x1 ; floor_to_test = (high+low)/2

    SUB  R11, R10, R9  ; diff = test - last

    CMP  R13, R11, R0
    BN   PH1_DIFF_DOWN
    BZ   PH1_DIFF_DOWN

PH1_DIFF_UP:
    ADD  R3, R3, R11
    CMP  R13, R0, R0
    BZ   PH1_DIFF_END

PH1_DIFF_DOWN:
    SUB  R13, R0, R11
    ADD  R4, R4, R13

PH1_DIFF_END:
    ADD  R9, R10, R0   ; last_floor = floor_to_test

    SUB  R11, R10, R2  ; diffTF
    CMP  R13, R11, R0
    BN   PH1_NO_BREAK
    BZ   PH1_NO_BREAK

PH1_BREAK:
    ADDI R5, 0x1
    SUBI R10, 0x1
    ADD  R7, R10, R0
    CMP  R13, R0, R0
    BZ   PHASE1_CHECK

PH1_NO_BREAK:
    ADD  R8, R10, R0
    CMP  R13, R0, R0
    BZ   PHASE1_CHECK

PHASE1_END:

; ===============================
; Phase 2
; ===============================

    SUB  R11, R7, R8
    CMP  R13, R11, R0
    BZ   PH2_EQUAL

    CMP  R13, R0, R0
    BZ   PH2_DIFF1

PH2_EQUAL:
    ADD  R12, R7, R0
    CMP  R13, R0, R0
    BZ   PHASE3_COST

PH2_DIFF1:
    ADDI R6, 0x01

    SUB  R11, R7, R9
    CMP  R13, R11, R0
    BN   PH2D1_DOWN
    BZ   PH2D1_DOWN

PH2D1_UP:
    ADD  R3, R3, R11
    CMP  R13, R0, R0
    BZ   PH2D1_DIFF_END

PH2D1_DOWN:
    SUB  R13, R0, R11
    ADD  R4, R4, R13

PH2D1_DIFF_END:
    SUB  R11, R7, R2
    CMP  R13, R11, R0
    BN   PH2D1_NOTBREAK
    BZ   PH2D1_NOTBREAK

PH2D1_BREAK:
    ADDI R5, 0x01
    ADD  R12, R8, R0
    CMP  R13, R0, R0
    BZ   PHASE3_COST

PH2D1_NOTBREAK:
    ADD  R12, R7, R0

; ===============================
; Phase 3 成本计算
; ===============================

PHASE3_COST:

    ; --- cost_scarcity = m*2 + n + h*4 ---
    ADD  R13, R3, R0
    SHLI R13, R13, 0x1     ; m*2
    ADD  R14, R13, R4

    ADD  R13, R5, R0
    SHLI R13, R13, 0x2     ; h*4
    ADD  R14, R14, R13

    ; --- cost_labor = m*4 + n + h*2 ---
    ADD  R13, R3, R0
    SHLI R13, R13, 0x2     ; m*4
    ADD  R15, R13, R4

    ADD  R13, R5, R0
    SHLI R13, R13, 0x1     ; h*2
    ADD  R15, R15, R13

    HALT
